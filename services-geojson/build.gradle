import org.jetbrains.kotlin.gradle.plugin.mpp.apple.XCFrameworkConfig

plugins {
    alias(libs.plugins.kotlin.multiplatform)
    alias(libs.plugins.kotlinx.serialization)
}

kotlin {
    jvmToolchain(17)

    jvm()

    def xcf = new XCFrameworkConfig(project)
    [iosX64(), iosArm64(), iosSimulatorArm64()].forEach { target ->
        target.binaries.framework { framework ->
            baseName = "maplibre-geojson"
            xcf.add(framework)
        }
    }

    sourceSets {
        commonMain.dependencies {
            api libs.kotlinx.serialization.json
        }

        commonTest.dependencies {
            implementation libs.kotlin.test
            implementation libs.kotlinx.io.core
        }

        jvmMain.dependencies {
            api libs.gson
        }
    }
}

tasks.register('copyiOSTestResources', Copy) {
    from "src/commonTest/resources"
    into "build/bin/iosX64/debugTest/resources"
}

tasks.named("iosX64Test") {
    dependsOn "copyiOSTestResources"
}